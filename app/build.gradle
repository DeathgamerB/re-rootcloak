apply plugin: 'com.android.application'

def keystorePass = hasProperty('KEYSTORE_PASS') ? KEYSTORE_PASS : System.getenv('KEYSTORE_PASS')
def aliasName = hasProperty('ALIAS_NAME') ? ALIAS_NAME : System.getenv('ALIAS_NAME')
def aliasPass = hasProperty('ALIAS_PASS') ? ALIAS_PASS : System.getenv('ALIAS_PASS')

android {
    compileSdkVersion 33
    buildToolsVersion "33.0.1"
    buildFeatures {
        buildConfig true
    }

    namespace "io.pfuenzle.rerootcloak"

    defaultConfig {
        applicationId "io.pfuenzle.rerootcloak"
        minSdkVersion 31
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
        ndk {
            moduleName "rootcloak"
            cFlags "-fPIC -shared"
            ldLibs "log"
        }
    }

    signingConfigs {
        release {
            storeFile file("release.keystore")
            storePassword keystorePass
            keyAlias aliasName
            keyPassword aliasPass
        }

    }

    lintOptions {
        disable 'MissingTranslation'
        disable 'ManifestResource'
        disable 'ExtraTranslation'
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
        }

        unsigneddebug {
            applicationIdSuffix ".debug"
            signingConfig null
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/libs']
            jni.srcDirs = ['src/main/jni']
        }
    }

    // call regular ndk-build(.cmd) script from app directory
    task ndkBuild(type: Exec) {
        workingDir file('src\\main')
        commandLine getNdkBuildCmd()
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }
}

def getNdkDir() {
    if (System.env.ANDROID_NDK_ROOT != null)
        return System.env.ANDROID_NDK_ROOT

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def ndkdir = properties.getProperty('ndk.dir', null)
    if (ndkdir == null)
        throw new GradleException("NDK location not found. Define location with ndk.dir in the local.properties file or with an ANDROID_NDK_ROOT environment variable.")

    return ndkdir
}

def getNdkBuildCmd() {
    def ndkbuild = getNdkDir() + "\\ndk-build"
    if (System.properties['os.name'].toLowerCase().contains('windows'))
        ndkbuild += ".cmd"

    return ndkbuild
}


dependencies {
    compileOnly 'de.robv.android.xposed:api:82'

    // The core module that provides APIs to a shell
    implementation "com.github.topjohnwu.libsu:core:${libsuVersion}"

    // Optional: APIs for creating root services. Depends on ":core"
    implementation "com.github.topjohnwu.libsu:service:${libsuVersion}"

    // Optional: Provides remote file system support
    implementation "com.github.topjohnwu.libsu:nio:${libsuVersion}"
}
